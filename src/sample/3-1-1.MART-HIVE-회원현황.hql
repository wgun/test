USE DEP;

-- 일별회원가입현황
CREATE EXTERNAL TABLE IF NOT EXISTS HMMB_D_MBER_SCRB_RPT
(
    STD_DT                 STRING COMMENT '기준일자'
  , SEX_CD                 STRING COMMENT '성별코드'
  , AGRDE_CD               STRING COMMENT '연령대코드'
  , MBRSH_GR_CD            STRING COMMENT '멤버십등급코드'
  , ACCUM_MBR_CNT          BIGINT COMMENT '누적회원수'
  , NEW_MBR_CNT            BIGINT COMMENT '신규회원수'
  , TERM_MBR_CNT           BIGINT COMMENT '해지회원수'
  , MKTG_PRA_AGRE_CNT      BIGINT COMMENT '마케팅활용동의건수'
  , VTLT_AGE_MSRT_CNT      BIGINT COMMENT 'VITALITY나이측정건수'
  , ACCUM_DVC_LNKG_CNT     BIGINT COMMENT '누적디바이스연동건수'
  , NEW_DVC_LNKG_CNT       BIGINT COMMENT '신규디바이스연동건수'
  , OPER_DTM               STRING COMMENT '작업일시'
)
COMMENT '일별회원가입현황'
PARTITIONED BY (dt STRING)
ROW FORMAT DELIMITED FIELDS TERMINATED BY '\001'
STORED AS ORC
LOCATION 'hdfs:///DEP/DATA/MART/SVC/STT/HMMB_D_MBER_SCRB_RPT'
TBLPROPERTIES ('ORC.COMPRESS'='SNAPPY');

INSERT OVERWRITE TABLE HMMB_D_MBER_SCRB_RPT PARTITION (DT = '20171120')
SELECT '20171120' AS STD_DT
     , A.SEX_CD
     , A.AGRDE_CD
     , A.MBRSH_GR_CD
     , SUM(IF(A.MBR_TERM_DT = '99991231', 1, 0))  AS ACCUM_MBR_CNT
     , SUM(IF(A.MBR_SCRB_DT = '20171120', 1, 0))  AS NEW_MBR_CNT
     , SUM(IF(A.MBR_TERM_DT = '20171120', 1, 0))  AS TERM_MBR_CNT
     , SUM(IF(A.MKTG_PRAPAGRE_YN = 'Y' AND A.MKTG_PRAPAGRE_DT = '20171120', 1, 0)) AS MKTG_PRA_AGRE_CNT
     , SUM(IF(A.VTLT_AGE_MSRT_YN = 'Y' AND A.VTLT_AGE_MSRT_DT = '20171120', 1, 0)) AS VTLT_AGE_MSRT_CNT
     , SUM(A.LNKG_DVC_CNT)                        AS ACCUM_DVC_LNKG_CNT
     , SUM(B.NEW_DVC_LNKG_CNT)                    AS NEW_DVC_LNKG_CNT
     , from_unixtime(unix_timestamp()) AS OPER_DTM
  FROM HMMB_D_MBER_DTL_LIST A
  JOIN (
        SELECT MBR_ID
             , SUM(IF(DVC_LNKG_INFO.DVC_LNKG_DT = '20171120', 1, 0)) AS NEW_DVC_LNKG_CNT
          FROM HMMB_D_MBER_DTL_LIST
        LATERAL VIEW EXPLODE(DVC_LNKG_INFO_ARR) D AS DVC_LNKG_INFO
         WHERE DT = '20171120'
         GROUP BY MBR_ID
       ) B
    ON A.MBR_ID = B.MBR_ID
 WHERE A.DT = '20171120'
 GROUP BY
       A.SEX_CD
     , A.AGRDE_CD
     , A.MBRSH_GR_CD
;
