-- Mobile AppLog DW
INSERT OVERWRITE TABLE HDSVC_DEP_APP_LOG_DW_T PARTITION (DT = '20171103')
SELECT SUBSTR(LOG_DTM, 1, 8) AS BASE_DT
     , GRP_ID
     , GRP_NO
     , LOG_DTM
     , unix_timestamp(LOG_DTM, 'yyyyMMddHHmmssSSS') - unix_timestamp(FIRST_LOG_DTM, 'yyyyMMddHHmmssSSS') AS DTIME_SEC
     , SESSION_ID
     , DEVICE_ID
     , MBR_NO
     , MDN_NO
     , PREV_PAGE_ID
     , CURR_PAGE_ID
     , CASE WHEN PREV_PAGE_ID IS NOT NULL AND CURR_PAGE_ID <> PREV_PAGE_ID THEN 1
            ELSE 0
        END AS PAGE_MV_CNT
     , INPATH_CD
  FROM (
        SELECT CONCAT(SESSION_ID, DEVICE_ID) AS GRP_ID
             , RANK() OVER(PARTITION BY CONCAT(SESSION_ID, DEVICE_ID) ORDER BY LOG_DTM) AS GRP_NO
             , LOG_DTM
             , FIRST_VALUE(LOG_DTM) OVER(PARTITION BY CONCAT(SESSION_ID, DEVICE_ID) ORDER BY LOG_DTM) AS FIRST_LOG_DTM
             , SESSION_ID
             , DEVICE_ID
             , MBR_NO
             , MDN_NO
             , LAG(CURR_PAGE_ID) OVER(PARTITION BY CONCAT(SESSION_ID, DEVICE_ID) ORDER BY LOG_DTM) AS PREV_PAGE_ID
             , CURR_PAGE_ID
             , INPATH_CD
          FROM HDSVC_DEP_APP_LOG_CDC_T
       ) T1;